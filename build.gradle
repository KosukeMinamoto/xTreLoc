plugins {
    id 'java'
    id 'application'
}

group = 'com.treloc.xtreloc'
version = '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_20
    targetCompatibility = JavaVersion.VERSION_20
}

ext {
    geotoolsVersion = '31.0'
}

repositories {
    mavenCentral().content {
        excludeModule("javax.media", "jai_core")
    }
    maven {
        url = 'https://repo.osgeo.org/repository/release/'
    }
    maven {
        url = 'https://repo.osgeo.org/repository/snapshot/'
    }
    maven {
        url = 'https://download.osgeo.org/webdav/geotools/'
    }
}

dependencies {
    // Commons Math 3
    implementation 'org.apache.commons:commons-math3:3.6.1'
    // Jackson Databind
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.1'
    // seisFile
    // implementation 'edu.sc.seis:seisFile:2.0.6'
    // JFreeChart
    implementation 'jfree:jfreechart:1.0.13'
    // JUnit
    testImplementation 'junit:junit:4.11'
    // TauP
    implementation 'edu.sc.seis:TauP:2.6.1'
    // GeographicLib Java
    implementation 'net.sf.geographiclib:GeographicLib-Java:1.49'
    // GeoTools dependencies
    implementation "org.geotools:gt-main:${geotoolsVersion}"
    implementation "org.geotools:gt-api:${geotoolsVersion}"
    implementation "org.geotools:gt-shapefile:${geotoolsVersion}"
    implementation "org.geotools:gt-swing:${geotoolsVersion}"
    implementation "org.geotools:gt-render:${geotoolsVersion}"
    implementation "org.geotools:gt-tile-client:${geotoolsVersion}"
    implementation "org.geotools:gt-grid:${geotoolsVersion}"
    implementation "org.geotools:gt-referencing:${geotoolsVersion}"
    
    // JTS for geometry operations
    implementation 'org.locationtech.jts:jts-core:1.19.0'
}

// GUI用JAR
jar {
    manifest {
        attributes(
                'Main-Class': 'com.treloc.xtreloc.app.gui.XTreLocGUI'
        )
    }
    // Fat JARを作成（すべての依存関係を含む）
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    // 署名ファイルを除外（SecurityExceptionを防ぐため）
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    archiveBaseName = 'xTreLoc-GUI'
}

// CLI用JAR
task cliJar(type: Jar) {
    group = 'build'
    description = 'Creates a CLI-only JAR file'
    archiveBaseName = 'xTreLoc-CLI'
    manifest {
        attributes(
                'Main-Class': 'com.treloc.xtreloc.app.cli.XTreLocCLI'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    from sourceSets.main.output
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    // 署名ファイルを除外（SecurityExceptionを防ぐため）
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainClass = 'com.treloc.xtreloc.app.gui.XTreLocGUI'
}

test {
    failOnNoDiscoveredTests = false
    useJUnit()
}

// CLI版を実行するためのタスク
task runCLI(type: JavaExec) {
    group = 'application'
    description = 'Run the CLI version of xTreLoc'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.treloc.xtreloc.app.cli.XTreLocCLI'
    args = project.hasProperty('cliArgs') ? project.cliArgs.tokenize() : ['--help']
}